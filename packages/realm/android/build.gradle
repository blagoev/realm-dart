group 'io.realm.realm_flutter'
version '1.0'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

apply plugin: RealmPlugin

class RealmPlugin implements Plugin<Project> {
    def debug = false;

    @Override
    void apply(Project project) {
        println "Root Project: ${project.getRootProject().name}"
        project.getRootProject().allprojects.each { prj ->
            println "project: ${prj.name}"
                if (debug) {
                    println "Project:" + prj.name
                }

                prj.configurations.each { conf ->
                    if (debug) {
                        println "  Configuration: ${conf.name}"
                    }
                    conf.allDependencies.each { dep ->
                        if (debug) {
                            println "    Dependency:      ${dep.group}:${dep.name}:${dep.version}"
                        }

                        if (dep.group && dep.group.equalsIgnoreCase("io.flutter") &&
                            dep.name && dep.name in ["armeabi_v7a_debug", "x86_debug", "x86_64_debug", "arm64_v8a_debug", "armeabi_v7a_release", "x86_release", "x86_64_release", "arm64_v8a_release"]) {
                            println "    Removing libflutter dependency: ${dep.group}:${dep.name}:${dep.version}"
                            conf.dependencies.remove(dep)
                        }
                    }
                }
        }
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
		ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
    }
	
	sourceSets {
        main {
            jniLibs.srcDirs += ["src/main/cpp/lib/"]
        }
    }
	
    lintOptions {
        disable 'InvalidPackage'
    }
}
